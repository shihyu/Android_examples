apply plugin: 'com.android.application'


Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def fabricDebugKey = properties.getProperty('FABRIC_DEBUG_KEY')
def fabricProductionKey = properties.getProperty('FABRIC_PRODUCTION_KEY')

android {
    compileSdkVersion compileSdkVer
    
    flavorDimensions "default"

    defaultConfig {
        applicationId "github.nisrulz.sample.buildvariants"
        minSdkVersion minSdkVer
        targetSdkVersion targetSdkVer
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [
                    fabricKey: fabricDebugKey
            ]
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            manifestPlaceholders = [
                    fabricKey: fabricProductionKey
            ]
        }
    }

    productFlavors {
        free {
            applicationIdSuffix ".free"
            manifestPlaceholders = [
                    appName: "Free App"
            ]
        }
        pro {
            applicationIdSuffix ".pro"
            manifestPlaceholders = [
                    appName: "Pro App"
            ]
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Testing-only dependencies
    testImplementation "junit:junit:$junitVer"
    androidTestImplementation "androidx.test:runner:$androidTestRunnerVer"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoCoreVer"

    implementation "androidx.appcompat:appcompat:$appCompat"
}
